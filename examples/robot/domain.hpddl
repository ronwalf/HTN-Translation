(define (domain robot)
  (:requirements :strips :negative-preconditions :typing)
  (:types PACKAGE ROOM ROOMDOOR)
  (:predicates 
    (armempty)
    (rloc ?loc - ROOM) 
    (in ?obj - PACKAGE ?loc - ROOM)
    (holding ?obj - PACKAGE)
    (closed ?d - ROOMDOOR)
    (door ?loc1 - ROOM ?loc2 - ROOM ?d - ROOMDOOR)
    
    (goal_in ?obj - PACKAGE ?loc - ROOM))
  (:tasks
    (achieve-goals)
    (release)
    (pickup ?obj - PACKAGE)
    (putdown)
    (move)
    (open))

(:action pickup
 :parameters (?obj - PACKAGE ?loc - ROOM)
 :task (pickup ?obj)
 :precondition (and (armempty) (rloc ?loc) (in ?obj ?loc)) 
 :effect (and (not (in ?obj ?loc)) (not (armempty)) (holding ?obj))
)

(:action putdown
 :parameters (?obj - PACKAGE ?loc - ROOM)
 :task (putdown)
 :precondition (and (rloc ?loc) (holding ?obj) (goal_in ?obj ?loc))
 :effect (and (not (holding ?obj)) (armempty) (in ?obj ?loc))
)

(:action move
 :parameters (?loc1 - ROOM ?loc2 - ROOM ?d - ROOMDOOR)
 :task (move)
 :precondition (and (rloc ?loc1) (door ?loc1 ?loc2 ?d) (not (closed ?d)))
 :effect (and (rloc ?loc2) (not (rloc ?loc1)))
)

(:action open
 :parameters (?loc1 - ROOM ?loc2 - ROOM ?d - ROOMDOOR)
 :task (open)
 :precondition (and (rloc ?loc1) (door ?loc1 ?loc2 ?d) (closed ?d))
 :effect (and (not (closed ?d)))
)

(:method release-putdown 
 :task (release)
 :precondition (exists (?loc - ROOM ?obj - PACKAGE) 
    (and 
        (rloc ?loc) 
        (holding ?obj)
        (goal_in ?obj ?loc)))
 :tasks ((putdown) (achieve-goals)))

(:method release-move
 :task (release)
 :tasks ((move) (release)))

(:method release-open
 :task (release)
 :tasks ((open) (release)))


(:method achieve-goals-pickup
 :parameters (?obj - PACKAGE)
 :task (achieve-goals)
 :precondition (exists (?loc - ROOM) (and 
        (rloc ?loc) 
        (in ?obj ?loc) 
        (not (goal_in ?obj ?loc))))
 :tasks ((pickup ?obj) (release)))

(:method achieve-goals-move
 :task (achieve-goals)
 :tasks ((move) (achieve-goals)))

(:method achieve-goals-open
 :task (achieve-goals)
 :tasks ((open) (achieve-goals)))


(:method finished
 :task (achieve-goals)
 :precondition (forall (?obj - PACKAGE) 
    (exists (?loc - ROOM) (and
        (goal_in ?obj ?loc)
        (in ?obj ?loc))))))
